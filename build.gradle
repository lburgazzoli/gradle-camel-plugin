// *****************************************************************************
// 
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

// *****************************************************************************
//
// *****************************************************************************

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'io.codearte.nexus-staging'

sourceCompatibility = 1.7
targetCompatibility = 1.7
group               = 'com.github.lburgazzoli'
description         = 'A gradle plugin to run Camel standalone<'

ext {
    gradleVersion    = '2.9'
    gradleScriptDir  = "${rootProject.projectDir}/gradle"
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    gitRoot          = "https://github.com/lburgazzoli"
    gitProject       = "https://github.com/lburgazzoli/${project.name}"
    inceptionYear    = '2015'

    if(!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
        nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
        nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
    }

    versions = [
        spock : '1.0-groovy-2.4'
    ]
}

repositories {
    mavenCentral()    
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile("org.spockframework:spock-core:$versions.spock") {
        exclude(module: 'groovy-all')
    }
}

jar {
    baseName = "${project.name}"

    manifest {
        attributes['Implementation-Title'   ] = "${group}.${project.name}-${project.version}"
        attributes['Implementation-Version' ] = project.version
        attributes['Implementation-Vendor'  ] = 'Luca Burgazzoli'
    }
}

// *****************************************************************************
//
// *****************************************************************************

publishing {
    repositories {
        maven {
            url isReleaseVersion ?
                    'https://oss.sonatype.org/service/local/staging/deploy/maven2/' :
                    'https://oss.sonatype.org/content/repositories/snapshots/'

            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }

    publications {
        camelPlugin(MavenPublication) {
            pom.withXml {
                asNode().with {
                    appendNode 'name', project.name
                    appendNode 'description', project.name
                    appendNode 'url', project.gitProject
                    appendNode 'inceptionYear', project.inceptionYear

                    appendNode('scm').with {
                        appendNode 'url', project.gitProject
                        appendNode 'connection', "scm:git:${project.gitProject}.git"
                        appendNode 'developerConnection', "scm:git:${project.gitProject}.git"
                    }

                    appendNode('licenses').appendNode('license').with {
                        appendNode 'name', 'The Apache Software License, Version 2.0'
                        appendNode 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        appendNode 'distribution', 'repo'
                    }

                    appendNode('developers').with {
                        appendNode('developer').with {
                            appendNode 'id', 'lburgazzoli'
                            appendNode 'name', 'Luca Burgazzoli'
                        }
                    }
                }
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

afterReleaseBuild.dependsOn publish

task nexusRelease(dependsOn: closeAndPromoteRepository) {
    println "Release Repo"
}
